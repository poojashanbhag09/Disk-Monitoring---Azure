InsightsMetrics
| where TimeGenerated >= ago(24h)
| where Origin == "vm.azm.ms" and Namespace == "LogicalDisk" and Name == "FreeSpaceMB"
| extend 
    Disk = tostring(todynamic(Tags)["vm.azm.ms/mountId"]),
    Disk_Size_GB = todouble(todynamic(Tags)["vm.azm.ms/diskSizeMB"]) / 1024
| summarize 
    Disk_Free_Space_GB = avg(Val) / 1024 
    by Computer, Disk, Disk_Size_GB, _ResourceId
| extend 
    Used_Space_GB = Disk_Size_GB - Disk_Free_Space_GB
| extend 
    Used_Space_Percentage = round((Used_Space_GB / Disk_Size_GB) * 100, 2),
    IsAzureVM = iff(_ResourceId has "Microsoft.Compute/virtualMachines", "Yes", "No")
| where Used_Space_Percentage >= 10
| project 
    Computer, Disk, Disk_Size_GB, Disk_Free_Space_GB, Used_Space_GB, ['Used Space %'] = Used_Space_Percentage, IsAzureVM
